#!/usr/bin/expect
# Copyright InMon Corp. 2015 ALL RIGHTS RESERVED
# send a sequence of configuration commands
proc sendcommands { switch user password commands } {

  spawn ssh $switch -l $user

  set timeout 30

  expect_after {
    timeout { catch { close }; wait; return 2 }
    eof { catch { close }; wait; return 1 }
  }

  expect {
   "(yes/no)? " {
      send "yes\r"
      exp_continue
   }
   "password: " {
      send "$password\r"
      exp_continue
   }
   ">" {
      send "enable\r"
      exp_continue
   }
   "Password:" {
      send "$password\r"
      exp_continue
   }
   "#" {
      send "configure terminal\r"
   }
  }
  expect "(config)#"

  foreach command $commands {
    send "$command\r"
    expect ")#"
  }

  send "exit\r"
  expect "#"
  send "exit\r"
  expect ">"
  send "exit\r"
  expect "closed."
  return 0;
}

set switchIP  [lindex $argv 0]
set user      [lindex $argv 1]
set password  [lindex $argv 2]
set type      [lindex $argv 3]
set operation [lindex $argv 4]

set commands [ list ]
if { [ string compare $type "if_block" ] == 0 } {
  set ifname [lindex $argv 5]
  if { [ string compare $operation "set" ] == 0 } {
    lappend commands "interface $ifname"
    lappend commands "disable"
    lappend commands "exit"
  } else {
    lappend commands "interface $ifname"
    lappend commands "enable"
    lappend commands "exit"
  }
} elseif { [ string compare $type "null_route" ] == 0 } {
  set victimIP [lindex $argv 5]
  if { [ string compare $operation "set" ] == 0 } {
    lappend commands "ip route $victimIP/32 null0 name \"DOS ATTACK\""
  } else {
    lappend commands "no ip route $victimIP/32 null0"
  }
}

exit [ sendcommands $switchIP $user $password $commands ]

